buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    def buildNumber = getBuildNumber()
    def versionNameString = getVersionName(buildNumber)

    defaultConfig {
        applicationId "io.nemesis.ninder"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode buildNumber
        versionName versionNameString
    }

    signingConfigs {
        release {
            storeFile file("release.jks")
            storePassword "nemesis++"
            keyAlias "ninder"
            keyPassword "ninder++"
        }
    }

    buildTypes {
        debug {
            // disable crashlytics
//            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release

            // enable crashlytics
            ext.enableCrashlytics = true

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //https://github.com/Diolor/Swipecards
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.vinaysshenoy:mugen:1.0.2'
    compile 'com.lorentzos.swipecards:library:1.0.8@aar'
    compile 'com.android.support:support-v13:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.robohorse.pagerbullet:pagerbullet:1.0.7'
    testCompile 'junit:junit:4.12'
}

def getBuildNumber() {
    def circleBranch = System.getenv("CIRCLE_BUILD_NUM")
    if (circleBranch != null && circleBranch.length() > 0) {
        // CircleCI build number
        return circleBranch.toInteger();
    }

    // fallback mechanism
    return 1
}

def getBranchName() {
    def branch = System.getenv("CIRCLE_BRANCH");
    if (branch != null && branch.length() > 0) {
        branch = branch.trim();
    } else {
        // fallback for local dev
        branch = "localdev";
    }

    return branch;
}

def getBuildNameSuffix() {
    def branch = getBranchName();

    if ("master".equalsIgnoreCase(branch)) {
        branch = "";
    }

    if (branch != null && branch.length() > 0) {
        return "-$branch";
    }

    return "";
}

def getVersionName(def buildNumber) {
    def nameSuffix = getBuildNameSuffix();

    def versionName = "1.0";
    if (!("".equalsIgnoreCase(nameSuffix))) {
        // no name suffix and build number on master branch build
        versionName = "$versionName.$buildNumber$nameSuffix";
    }

    return versionName;
}